-- Adopted from https://github.com/topfunky/osx-window-sizing
-- 50% written by Geoffrey Grosenbach (http://peepcode.com)
-- 50% written by Thomas Redding (http://ThomasRedding.com)

-- new (x,y) position of window
set x to -1
set y to 0.5

-- new width and height of window
set w to -1
set h to 0.5

-- note: if any of these 4 variables are set to -1,
-- then they take the window's current corresponding value

tell application "Finder"
    set {screen_left, screen_top, screen_width, screen_height} to bounds of window of desktop
end tell

tell application "System Events"
    set myFrontMost to name of first item of (processes whose frontmost is true)
    tell process myFrontMost
        set {old_w, old_h} to get size of window 1
        set {old_x, old_y} to get position of window 1
        if x is equal to -1 then
            set x to old_x / screen_width
        end if
        if y is equal to -1 then
            set y to old_y / screen_height
        end if
        if w is equal to -1 then
            set w to old_w / screen_width
        end if
        if h is equal to -1 then
            set h to old_h / screen_height
        end if
        set size of window 1 to {(screen_width * w), (screen_height * h)}
        set position of window 1 to {(screen_width * x), (screen_height * y)}
        set size of window 1 to {(screen_width * w), (screen_height * h)}
    end tell
end tell